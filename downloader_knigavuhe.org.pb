; –ü–µ—Ä–µ—á–∏—Å–ª–µ–Ω–∏–µ –∏–¥–µ–Ω—Ç–∏—Ñ–∏–∫–∞—Ç–æ—Ä–æ–≤ –æ–∫–æ–Ω
Enumeration FormWindow
  #main_window
EndEnumeration

; –ü–µ—Ä–µ—á–∏—Å–ª–µ–Ω–∏–µ –∏–¥–µ–Ω—Ç–∏—Ñ–∏–∫–∞—Ç–æ—Ä–æ–≤ —ç–ª–µ–º–µ–Ω—Ç–æ–≤ –∏–Ω—Ç–µ—Ä—Ñ–µ–π—Å–∞
Enumeration FormGadget
  #npt_url          ; –ü–æ–ª–µ –≤–≤–æ–¥–∞ URL
  #npt_path         ; –ü–æ–ª–µ –≤–≤–æ–¥–∞ –ø—É—Ç–∏ —Å–æ—Ö—Ä–∞–Ω–µ–Ω–∏—è
  #btn_path         ; –ö–Ω–æ–ø–∫–∞ –≤—ã–±–æ—Ä–∞ –ø—É—Ç–∏
  #lbl_authors      ; –ú–µ—Ç–∫–∞ "–ê–≤—Ç–æ—Ä(—ã)"
  #lbl_readers      ; –ú–µ—Ç–∫–∞ "–ß—Ç–µ—Ü(—ã)"
  #progress_bar     ; –ü—Ä–æ–≥—Ä–µ—Å—Å-–±–∞—Ä –∑–∞–≥—Ä—É–∑–∫–∏
  #src_book_name    ; –ü–æ–ª–µ –æ—Ç–æ–±—Ä–∞–∂–µ–Ω–∏—è –Ω–∞–∑–≤–∞–Ω–∏—è –∫–Ω–∏–≥–∏
  #src_authors      ; –ü–æ–ª–µ –æ—Ç–æ–±—Ä–∞–∂–µ–Ω–∏—è –∞–≤—Ç–æ—Ä–æ–≤
  #src_readers      ; –ü–æ–ª–µ –æ—Ç–æ–±—Ä–∞–∂–µ–Ω–∏—è —á—Ç–µ—Ü–æ–≤
  #src_info         ; –ò–Ω—Ñ–æ—Ä–º–∞—Ü–∏–æ–Ω–Ω–æ–µ –ø–æ–ª–µ (—Å—Ç–∞—Ç—É—Å)
  #tracker          ; –¢—Ä–µ–∫–µ—Ä –∑–∞–≥—Ä—É–∑–∫–∏ (—Ä–∞–∑–º–µ—Ä —Ñ–∞–π–ª–∞)
  #btn_down         ; –ö–Ω–æ–ø–∫–∞ —Å–∫–∞—á–∏–≤–∞–Ω–∏—è/–ø—Ä–µ—Ä—ã–≤–∞–Ω–∏—è
EndEnumeration

; –°—Ç—Ä—É–∫—Ç—É—Ä–∞ –¥–ª—è –æ–±—Ä–∞–±–æ—Ç–∫–∏ –æ—à–∏–±–æ–∫
Structure err
  isErr.b           ; –§–ª–∞–≥ –Ω–∞–ª–∏—á–∏—è –æ—à–∏–±–∫–∏ (0/1)
EndStructure

; –°—Ç—Ä—É–∫—Ç—É—Ä–∞ –¥–ª—è —Ö—Ä–∞–Ω–µ–Ω–∏—è –∏–º–µ–Ω (–∞–≤—Ç–æ—Ä–æ–≤/—á—Ç–µ—Ü–æ–≤)
Structure names
  name.s            ; –°—Ç—Ä–æ–∫–∞ —Å –∏–º–µ–Ω–µ–º
EndStructure

; –°—Ç—Ä—É–∫—Ç—É—Ä–∞ –¥–∞–Ω–Ω—ã—Ö –æ –∫–Ω–∏–≥–µ
Structure book
  name.s            ; –ù–∞–∑–≤–∞–Ω–∏–µ –∫–Ω–∏–≥–∏
  Map authors.names() ; –ê—Å—Å–æ—Ü–∏–∞—Ç–∏–≤–Ω—ã–π –º–∞—Å—Å–∏–≤ –∞–≤—Ç–æ—Ä–æ–≤
  Map readers.names() ; –ê—Å—Å–æ—Ü–∏–∞—Ç–∏–≤–Ω—ã–π –º–∞—Å—Å–∏–≤ —á—Ç–µ—Ü–æ–≤
  cover.s           ; URL –æ—Å–Ω–æ–≤–Ω–æ–π –æ–±–ª–æ–∂–∫–∏
  cover_alt.s       ; URL –∞–ª—å—Ç–µ—Ä–Ω–∞—Ç–∏–≤–Ω–æ–π –æ–±–ª–æ–∂–∫–∏
  url.s             ; URL –∫–Ω–∏–≥–∏
EndStructure

; –°—Ç—Ä—É–∫—Ç—É—Ä–∞ –¥–∞–Ω–Ω—ã—Ö –∞—É–¥–∏–æ—Ñ–∞–π–ª–∞
Structure audio
  title.s           ; –ù–∞–∑–≤–∞–Ω–∏–µ —Ç—Ä–µ–∫–∞
  url.s             ; URL —Ç—Ä–µ–∫–∞
EndStructure

; –û—Å–Ω–æ–≤–Ω–∞—è —Å—Ç—Ä—É–∫—Ç—É—Ä–∞ –¥–∞–Ω–Ω—ã—Ö –ø—Ä–∏–ª–æ–∂–µ–Ω–∏—è
Structure main
  id.i              ; –ò–¥–µ–Ω—Ç–∏—Ñ–∏–∫–∞—Ç–æ—Ä
  book.book         ; –î–∞–Ω–Ω—ã–µ –∫–Ω–∏–≥–∏
  List playlist.audio()       ; –û—Å–Ω–æ–≤–Ω–æ–π –ø–ª–µ–π–ª–∏—Å—Ç
  List merged_playlist.audio(); –ê–ª—å—Ç–µ—Ä–Ω–∞—Ç–∏–≤–Ω—ã–π –ø–ª–µ–π–ª–∏—Å—Ç
EndStructure

; –°—Ç—Ä—É–∫—Ç—É—Ä–∞ –¥–ª—è —Ö—Ä–∞–Ω–µ–Ω–∏—è HTML-—Å—Ç—Ä–∞–Ω–∏—Ü—ã
Structure page
  html.s            ; HTML-–∫–æ–¥ —Å—Ç—Ä–∞–Ω–∏—Ü—ã
EndStructure

; –ì–ª–æ–±–∞–ª—å–Ω—ã–µ –ø–µ—Ä–µ–º–µ–Ω–Ω—ã–µ
Global mainStruct.main, stop.b, downloading

; –ü—Ä–æ—Ü–µ–¥—É—Ä–∞ –æ–±–Ω–æ–≤–ª–µ–Ω–∏—è —Ñ–∞–π–ª–∞ –æ–ø–∏—Å–∞–Ω–∏—è
Procedure updateDecription(path$, text$)
  If OpenFile(0, path$)
    FileSeek(0, Lof(0))  ; –ü–µ—Ä–µ–º–µ—â–∞–µ–º—Å—è –≤ –∫–æ–Ω–µ—Ü —Ñ–∞–π–ª–∞
    WriteStringN(0, text$)
    CloseFile(0)
  EndIf
EndProcedure

; –ü—Ä–æ—Ü–µ–¥—É—Ä–∞ –ø–æ–∏—Å–∫–∞ –ø–æ —Ä–µ–≥—É–ª—è—Ä–Ω–æ–º—É –≤—ã—Ä–∞–∂–µ–Ω–∏—é
Procedure.s findRegExpOne(text$, exp$, *err.err)
  Protected result$, re
  
  ; –°–æ–∑–¥–∞–µ–º —Ä–µ–≥—É–ª—è—Ä–Ω–æ–µ –≤—ã—Ä–∞–∂–µ–Ω–∏–µ
  re = CreateRegularExpression(#PB_Any, exp$, #PB_RegularExpression_DotAll)
  If re
    If ExamineRegularExpression(re, text$)
      NextRegularExpressionMatch(re)
      result$ = RegularExpressionGroup(re, 1)  ; –ò–∑–≤–ª–µ–∫–∞–µ–º –ø–µ—Ä–≤—É—é –≥—Ä—É–ø–ø—É
    Else
      *err\isErr = 1
      result$ = "–û—à–∏–±–∫–∞ —Ä–µ–≥—É–ª—è—Ä–Ω–æ–≥–æ –≤—ã—Ä–∞–∂–µ–Ω–∏—è:" + RegularExpressionError()
    EndIf
  Else
    *err\isErr = 1
    result$ = "–û—à–∏–±–∫–∞ —Ä–µ–≥—É–ª—è—Ä–Ω–æ–≥–æ –≤—ã—Ä–∞–∂–µ–Ω–∏—è:" + RegularExpressionError()
  EndIf
  
  FreeRegularExpression(re)
  ProcedureReturn result$
EndProcedure

; –ü—Ä–æ—Ü–µ–¥—É—Ä–∞ –∏–∑–≤–ª–µ—á–µ–Ω–∏—è —á–∞—Å—Ç–∏ —Å—Ç—Ä–æ–∫–∏ –ø–æ —Ä–∞–∑–¥–µ–ª–∏—Ç–µ–ª—é
Procedure.s getPartString(s$, sep$, idx=0)
  Protected c
  s$ = RTrim(s$, sep$)
  c = CountString(s$, sep$)
  
  If idx = -1
    ProcedureReturn StringField(s$, c+1, sep$)  ; –ü–æ—Å–ª–µ–¥–Ω—è—è —á–∞—Å—Ç—å
  ElseIf idx = 0
    ProcedureReturn s$                          ; –í—Å—è —Å—Ç—Ä–æ–∫–∞
  EndIf 
  
  ProcedureReturn StringField(s$, c+1, sep$)    ; –ö–æ–Ω–∫—Ä–µ—Ç–Ω–∞—è —á–∞—Å—Ç—å
EndProcedure

; –ü—Ä–æ—Ü–µ–¥—É—Ä–∞ –ø–æ–¥–≥–æ—Ç–æ–≤–∫–∏ JSON –¥–∞–Ω–Ω—ã—Ö
Procedure.b prepareJSON(json$)
  If ParseJSON(0, json$)
    ExtractJSONStructure(JSONValue(0), mainStruct.main, main)
    FreeJSON(0)
    ProcedureReturn #True
  EndIf 
  ProcedureReturn #False
EndProcedure

; –ü—Ä–æ—Ü–µ–¥—É—Ä–∞ –∑–∞–≥—Ä—É–∑–∫–∏ –≤–µ–±-—Å—Ç—Ä–∞–Ω–∏—Ü—ã
Procedure.s downloadPage(url$, *err.err)
  Protected html$, NewMap Header$()
  ; –£—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –∑–∞–≥–æ–ª–æ–≤–∫–∏ –∑–∞–ø—Ä–æ—Å–∞
  Header$("cookie") = "new_design=1"
  Header$("User-Agent") = "Mozilla/5.0 (Windows NT 10.0; Win64; x64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/135.0.0.0 Safari/537.36 Edg/135.0.0.0"
  
  HttpRequest = HTTPRequest(#PB_HTTP_Get, url$, "", 0, Header$())
  If HttpRequest
    If HTTPInfo(HTTPRequest, #PB_HTTP_StatusCode) = "200"
      *BufPage = HTTPMemory(HTTPRequest)
      html$ = PeekS(*BufPage, MemorySize(*BufPage), #PB_UTF8|#PB_ByteLength)
      FreeMemory(*BufPage)
    Else
      *err\isErr = 1
      html$ = ~"–û—à–∏–±–∫–∞ –≤—ã–ø–æ–ª–Ω–µ–Ω–∏—è –∑–∞–ø—Ä–æ—Å–∞: \n"+url$+~"\n"+HTTPInfo(HTTPRequest, #PB_HTTP_ErrorMessage)
    EndIf 
  Else
    *err\isErr = 1
    html$ = ~"–û—à–∏–±–∫–∞ —Å–æ–∑–¥–∞–Ω–∏—è –∑–∞–ø—Ä–æ—Å–∞: \n"+url$
  EndIf 
  
  FinishHTTP(HttpRequest)
  FreeMap(Header$())
  ProcedureReturn html$
EndProcedure

; –ü—Ä–æ—Ü–µ–¥—É—Ä–∞ —Ñ–æ—Ä–º–∞—Ç–∏—Ä–æ–≤–∞–Ω–∏—è —Å–ø–∏—Å–∫–∞ –∞–≤—Ç–æ—Ä–æ–≤/—á—Ç–µ—Ü–æ–≤
Procedure.s getAuthors(Map tMap.names())
  Protected authors$
  ResetMap(tMap())
  While NextMapElement(tMap())
    If Len(authors$) > 0
      authors$ + ", " + tMap()\name  ; –î–æ–±–∞–≤–ª—è–µ–º —Ä–∞–∑–¥–µ–ª–∏—Ç–µ–ª—å –¥–ª—è –Ω–µ—Å–∫–æ–ª—å–∫–∏—Ö –∏–º–µ–Ω
    Else
      authors$ = tMap()\name         ; –ü–µ—Ä–≤–æ–µ –∏–º—è –≤ —Å–ø–∏—Å–∫–µ
    EndIf
  Wend
  ProcedureReturn authors$
EndProcedure

; –û—Å–Ω–æ–≤–Ω–∞—è –ø—Ä–æ—Ü–µ–¥—É—Ä–∞ –æ–±—Ä–∞–±–æ—Ç–∫–∏ —Å—Ç—Ä–∞–Ω–∏—Ü—ã
Procedure.s processPage(*page.page, *err.err)
  Protected json$, author$, reader$
  ; –ò—â–µ–º JSON –¥–∞–Ω–Ω—ã–µ –≤ HTML
  json$ = findRegExpOne(*page\html, "BookController\.enter\((.*?\})\);", *err)
  
  If json$
    If Not prepareJSON(json$)  ; –ü–∞—Ä—Å–∏–º JSON
      ProcedureReturn ~"–ù–µ —É–¥–∞–ª–æ—Å—å –æ–±—Ä–∞–±–æ—Ç–∞—Ç—å JSON:\n" + json$
    EndIf 
    
    ; –§–æ—Ä–º–∏—Ä—É–µ–º –∏–º—è —Ñ–∞–π–ª–∞ –¥–ª—è –∞–ª—å—Ç–µ—Ä–Ω–∞—Ç–∏–≤–Ω–æ–π –æ–±–ª–æ–∂–∫–∏
    mainStruct\book\cover_alt = StringField(mainStruct\book\cover, 1, "-") + ".jpg"
    
    ; –û–±–Ω–æ–≤–ª—è–µ–º –∏–Ω—Ç–µ—Ä—Ñ–µ–π—Å
    SetGadgetText(#src_book_name, mainStruct\book\name)
    
    author$ = getAuthors(mainStruct\book\authors())
    SetGadgetText(#src_authors, author$)
    
    ; –£—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø—Ä–∞–≤–∏–ª—å–Ω—É—é —Ñ–æ—Ä–º—É —Å–ª–æ–≤–∞ "–ê–≤—Ç–æ—Ä(—ã)"
    If FindString(author$, ", ")
      SetGadgetText(#lbl_authors, "–ê–≤—Ç–æ—Ä—ã:")
    Else
      SetGadgetText(#lbl_authors, "–ê–≤—Ç–æ—Ä:")
    EndIf 
    
    reader$ = getAuthors(mainStruct\book\readers())
    SetGadgetText(#src_readers, reader$)
    
    ; –£—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø—Ä–∞–≤–∏–ª—å–Ω—É—é —Ñ–æ—Ä–º—É —Å–ª–æ–≤–∞ "–ß—Ç–µ—Ü(—ã)"
    If FindString(reader$, ", ")
      SetGadgetText(#lbl_readers, "–ß—Ç–µ—Ü—ã:")
    Else
      SetGadgetText(#lbl_readers, "–ß—Ç–µ—Ü:")
    EndIf 
    
  Else
    ProcedureReturn ~"–ù–µ —É–¥–∞–ª–æ—Å—å –ø–æ–ª—É—á–∏—Ç—å JSON:\n" + json$
  EndIf
  
  ProcedureReturn ""
EndProcedure

; –û–±—Ä–∞–±–æ—Ç—á–∏–∫ –∫–Ω–æ–ø–∫–∏ –≤—ã–±–æ—Ä–∞ –ø—É—Ç–∏
Procedure path_button()
  Protected defaultPath$, path$
  defaultPath$ = GetEnvironmentVariable("USERPROFILE") + "\Music\"  
  path$ = PathRequester("–í—ã–±–µ—Ä–∏—Ç–µ –ø–∞–ø–∫—É –¥–ª—è —Å—Ö—Ä–∞–Ω–µ–Ω–∏—è", defaultPath$)
  
  If path$ 
    SetGadgetText(#npt_path, path$)
    ProcedureReturn
  EndIf
  
  If Len(GetGadgetText(#npt_path)) = 0
    MessageRequester("–ü—Ä–µ–¥—É–ø—Ä–µ–∂–¥–µ–Ω–∏–µ", "–ü—É—Ç—å —Å–æ—Ö—Ä–∞–Ω–µ–Ω–∏—è –Ω–µ –º–æ–∂–µ—Ç –±—ã—Ç—å –ø—É—Å—Ç—ã–º", #PB_MessageRequester_Warning)
  EndIf 
EndProcedure

; –§–æ—Ä–º–∞—Ç–∏—Ä–æ–≤–∞–Ω–∏–µ —Ä–∞–∑–º–µ—Ä–∞ —Ñ–∞–π–ª–∞
Procedure.s ConvertBytes(bytes)
  Protected result.s
  If bytes >= 1048576
    result = StrF(bytes / 1048576.0, 2) + "mb"
  ElseIf bytes >= 1024
    result = StrF(bytes / 1024.0, 2) + "kb"
  Else
    result = Str(bytes) + "b"
  EndIf
  ProcedureReturn result
EndProcedure

; –ü–æ–ª—É—á–µ–Ω–∏–µ —Ä–∞–∑–º–µ—Ä–∞ —Ñ–∞–π–ª–∞ –ø–æ URL
Procedure GetContentLength(url$)
  Protected file_size, Header$
  HttpRequest = HTTPRequest(#PB_HTTP_Get, url$, "", #PB_HTTP_HeadersOnly)
  If HttpRequest
    Header$ = HTTPInfo(HTTPRequest, #PB_HTTP_Headers)
    Header$ = StringField(Header$, 2, "Content-Length:")
    file_size = Val(StringField(Header$, 1, #LF$))
    FinishHTTP(HTTPRequest)
  EndIf 
  ProcedureReturn file_size
EndProcedure

; –ü—Ä–æ—Ü–µ–¥—É—Ä–∞ –∑–∞–≥—Ä—É–∑–∫–∏ —Ñ–∞–π–ª–∞ —Å –æ—Ç—Å–ª–µ–∂–∏–≤–∞–Ω–∏–µ–º –ø—Ä–æ–≥—Ä–µ—Å—Å–∞
Procedure.s downFile(url$, name_file$, size_file=0)
  Protected content_length, Download, Progress, result$
  
  Download = ReceiveHTTPFile(url$, name_file$, #PB_HTTP_Asynchronous)
  If Download
    Repeat
      If stop
        AbortHTTP(Download)
      EndIf 
      
      Progress = HTTPProgress(Download)
      Select Progress
        Case #PB_HTTP_Success
          Break
          
        Case #PB_HTTP_Failed
          result$ = "–ó–∞–≥—Ä—É–∑–∫–∞ –Ω–µ —É–¥–∞–ª–∞—Å—å"
          Break
          
        Case #PB_HTTP_Aborted
          result$ = "–ó–∞–≥—Ä—É–∑–∫–∞ –ø—Ä–µ—Ä–≤–∞–Ω–∞"
          Break
          
        Default
          If size_file
            SetGadgetText(#tracker, ConvertBytes(Progress)+ "/" +ConvertBytes(size_file))
            SetGadgetState(#progress_bar, Int(Progress/size_file*100))            
          EndIf 
      EndSelect
      
      Delay(50)
    ForEver
    FinishHTTP(Download)
  Else
    result$ = "–û—à–∏–±–∫–∞ –∑–∞–≥—Ä—É–∑–∫–∏"
  EndIf
  
  If size_file
    SetGadgetText(#tracker, ConvertBytes(size_file)+ "/" +ConvertBytes(size_file))
    SetGadgetState(#progress_bar, 0)
  EndIf
  ProcedureReturn result$
EndProcedure

; –£–¥–∞–ª–µ–Ω–∏–µ —Ñ–∞–π–ª–æ–≤ –ø–æ —Ä–∞—Å—à–∏—Ä–µ–Ω–∏—é
Procedure delFilesByExt(path$, ext$)
  If ExamineDirectory(0, path$, ext$)
    While NextDirectoryEntry(0)
      If DirectoryEntryType(0) = #PB_DirectoryEntry_File
        file$ = path$ + DirectoryEntryName(0)
        If Not DeleteFile(file$)
          RenameFile(file$, file$+".delete")
        EndIf
      EndIf
    Wend
    FinishDirectory(0)
  Else
    MessageRequester("–û—à–∏–±–∫–∞", "–ù–µ —É–¥–∞–ª–æ—Å—å –æ—Ç–∫—Ä—ã—Ç—å –ø–∞–ø–∫—É: " + directory$)
  EndIf
EndProcedure

; –ü—Ä–æ–≤–µ—Ä–∫–∞ —Å—É—â–µ—Å—Ç–≤–æ–≤–∞–Ω–∏—è —Ñ–∞–π–ª–∞
Procedure.b fileExists(path$, name_file$, size_file=0)
  Protected result.b
  If ExamineDirectory(0, path$, name_file$)
    If NextDirectoryEntry(0)
      If Not size_file
        result = #True
      ElseIf DirectoryEntryType(0) = #PB_DirectoryEntry_File  
        If size_file = DirectoryEntrySize(0)
          result = #True
        EndIf
      EndIf
    EndIf 
    FinishDirectory(0)
  Else
    MessageRequester("–û—à–∏–±–∫–∞", "–ù–µ —É–¥–∞–ª–æ—Å—å –æ—Ç–∫—Ä—ã—Ç—å –ø–∞–ø–∫—É: " + directory$)
  EndIf
  ProcedureReturn result
EndProcedure

; –ò—Ç–µ—Ä–∞—Ü–∏—è –ø–æ –ø–ª–µ–π–ª–∏—Å—Ç—É –∏ –∑–∞–≥—Ä—É–∑–∫–∞ —Ç—Ä–µ–∫–æ–≤
Procedure.s iterPlaylist(path$, List pl.audio())
  Protected name_file$, path_file$, count, total$, error$, ext$, size_file
  total$ = Str(ListSize(pl()))
  
  ResetList(pl())
  ForEach pl()
    If stop
      error$ = "–ó–∞–≥—Ä—É–∑–∫–∞ –ø—Ä–µ—Ä–≤–∞–Ω–∞"
      Break
    EndIf 
    
    count +1
    SetGadgetText(#src_info, Str(count) + "/" + total$ + " " + pl()\title)
    
    ; –û—á–∏—â–∞–µ–º URL –æ—Ç –ø–∞—Ä–∞–º–µ—Ç—Ä–æ–≤
    pl()\url = getPartString(pl()\url, "?", 1)
    ext$ = "."+getPartString(pl()\url, ".", -1)
    name_file$ = pl()\title + ext$
    
    path_file$ = path$ + name_file$
    size_file = GetContentLength(pl()\url)
    
    ; –ü—Ä–æ–ø—É—Å–∫–∞–µ–º —É–∂–µ —Å–∫–∞—á–∞–Ω–Ω—ã–µ —Ñ–∞–π–ª—ã
    If fileExists(path$, name_file$, size_file)
      SetGadgetText(#tracker, "–£–∂–µ —Å–∫–∞—á–∞–Ω")
      Continue
    EndIf 
    
    ; 3 –ø–æ–ø—ã—Ç–∫–∏ —Å–∫–∞—á–∏–≤–∞–Ω–∏—è
    For i=1 To 3
      error$ = downFile(pl()\url, path_file$, size_file)
      If Len(error$) = 0
        Break
      EndIf 
      SetGadgetText(#tracker, "–ü–æ–ø—ã—Ç–∫–∞ " + Str(i))
    Next
      
    If Len(error$) <> 0
      error$ + ~"\n" + "–°–∫–∞—á–∞–Ω–æ " + Str(count-1) + " –∞—É–¥–∏–æ—Ñ–∞–π–ª–æ–≤ –∏–∑ " + total$
      Break
    EndIf 
  Next
  ProcedureReturn error$
EndProcedure

; –û—Å–Ω–æ–≤–Ω–∞—è –ø—Ä–æ—Ü–µ–¥—É—Ä–∞ –∑–∞–≥—Ä—É–∑–∫–∏ –ø–ª–µ–π–ª–∏—Å—Ç–∞
Procedure.s downPlaylist(path$)
  Protected error$
  
  ; –ü—Ä–æ–±—É–µ–º –æ—Å–Ω–æ–≤–Ω–æ–π –ø–ª–µ–π–ª–∏—Å—Ç
  error$ = iterPlaylist(path$, mainStruct\playlist())
  If Len(error$) = 0 Or stop
    ProcedureReturn error$
  EndIf 
  
  ; –ï—Å–ª–∏ –æ—à–∏–±–∫–∞ - –ø—Ä–æ–±—É–µ–º –∞–ª—å—Ç–µ—Ä–Ω–∞—Ç–∏–≤–Ω—ã–π –ø–ª–µ–π–ª–∏—Å—Ç
  error$ + ~"\n\n –ó–∞–≥—Ä—É–∑–∫–∞ –Ω–µ—á–Ω–µ—Ç—Å—è –∑–∞–Ω–æ–≤–æ –∏–∑ –∞–ª—å—Ç–µ—Ä–Ω–∞—Ç–∏–≤–Ω–æ–≥–æ –∏—Å—Ç–æ—á–Ω–∏–∫–∞"
  MessageRequester("–û—à–∏–±–∫–∞", error$, #PB_MessageRequester_Error)
  
  delFilesByExt(path$, "*.mp3")
  
  error$ = iterPlaylist(path$, mainStruct\merged_playlist())
  
  If Len(error$) <> 0
    error$ + ~"\n\n –ó–∞–≥—Ä—É–∑–∫–∞ –Ω–µ–≤–æ–∑–º–æ–∂–Ω–∞ –∏–∑ –æ–±–æ–∏—Ö –∏—Å—Ç–æ—á–Ω–∏–∫–æ–≤ :("
  EndIf 
  
  ProcedureReturn error$
EndProcedure

; –°–æ–∑–¥–∞–Ω–∏–µ –ø–æ–ª–Ω–æ–≥–æ –ø—É—Ç–∏ —Å –ø—Ä–æ–≤–µ—Ä–∫–æ–π –∏ —Å–æ–∑–¥–∞–Ω–∏–µ–º –¥–∏—Ä–µ–∫—Ç–æ—Ä–∏–π
Procedure.s CreateFullPath(path$)
  Protected Parts$, i, CurrentPath$
  
  ; –ù–æ—Ä–º–∞–ª–∏–∑—É–µ–º –ø—É—Ç—å
  If Right(path$, 1) = "\" Or Right(path$, 1) = "/"
    path$ = Left(path$, Len(path$) - 1)
  EndIf
  
  path$ = ReplaceString(path$, "/", "\")
  
  ; –ü–æ—Å—Ç–µ–ø–µ–Ω–Ω–æ —Å–æ–∑–¥–∞–µ–º –≤—Å–µ –ø–æ–¥–¥–∏—Ä–µ–∫—Ç–æ—Ä–∏–∏
  Parts$ = StringField(path$, 1, "\")
  CurrentPath$ = Parts$
  
  For i = 2 To CountString(path$, "\") + 1
    Parts$ = StringField(path$, i, "\")
    CurrentPath$ + "\" + Parts$
    If FileSize(CurrentPath$) <> -2
      CreateDirectory(CurrentPath$)
    EndIf
  Next
  
  ProcedureReturn path$ + "\"  
EndProcedure

Procedure.s SanitizePath(path$)
  ; –°–ø–∏—Å–æ–∫ –Ω–µ–±–µ–∑–æ–ø–∞—Å–Ω—ã—Ö —Å–∏–º–≤–æ–ª–æ–≤ –≤ –ø—É—Ç—è—Ö Windows
  Protected forbiddenChars$ = "\/:*!?<>|" + Chr(34) ; + –∫–∞–≤—ã—á–∫–∏
  Protected i, char$, safePath$
  
  ; –ü—Ä–æ–≤–µ—Ä—è–µ–º –∫–∞–∂–¥—ã–π —Å–∏–º–≤–æ–ª –≤ –ø—É—Ç–∏
  For i = 1 To Len(path$)
    char$ = Mid(path$, i, 1)
    
    ; –ó–∞–º–µ–Ω—è–µ–º –Ω–µ–±–µ–∑–æ–ø–∞—Å–Ω—ã–µ —Å–∏–º–≤–æ–ª—ã –Ω–∞ –ø–æ–¥—á–µ—Ä–∫–∏–≤–∞–Ω–∏–µ
    If FindString(forbiddenChars$, char$)
      safePath$ + "_"
    Else
      safePath$ + char$
    EndIf
  Next
  
  ; –£–¥–∞–ª—è–µ–º –≤–µ–¥—É—â–∏–µ –∏ –∑–∞–≤–µ—Ä—à–∞—é—â–∏–µ –ø—Ä–æ–±–µ–ª—ã –∏ —Ç–æ—á–∫–∏ (–æ—Å–æ–±–µ–Ω–Ω–æ –≤–∞–∂–Ω–æ –¥–ª—è Windows)
  safePath$ = Trim(safePath$)
  While Right(safePath$, 1) = "."
    safePath$ = Left(safePath$, Len(safePath$) - 1)
  Wend
  
  ; –ü—Ä–æ–≤–µ—Ä—è–µ–º –∑–∞—Ä–µ–∑–µ—Ä–≤–∏—Ä–æ–≤–∞–Ω–Ω—ã–µ –∏–º–µ–Ω–∞ —É—Å—Ç—Ä–æ–π—Å—Ç–≤ Windows
  Protected fileName$ = GetFilePart(safePath$)
  Select UCase(fileName$)
    Case "CON", "PRN", "AUX", "NUL", 
         "COM1", "COM2", "COM3", "COM4", "COM5", "COM6", "COM7", "COM8", "COM9",
         "LPT1", "LPT2", "LPT3", "LPT4", "LPT5", "LPT6", "LPT7", "LPT8", "LPT9"
      safePath$ + "_" ; –î–æ–±–∞–≤–ª—è–µ–º –ø–æ–¥—á–µ—Ä–∫–∏–≤–∞–Ω–∏–µ –∫ –∑–∞—Ä–µ–∑–µ—Ä–≤–∏—Ä–æ–≤–∞–Ω–Ω—ã–º –∏–º–µ–Ω–∞–º
  EndSelect
  
  ProcedureReturn safePath$
EndProcedure

; –ü–æ–¥–≥–æ—Ç–æ–≤–∫–∞ –¥–∏—Ä–µ–∫—Ç–æ—Ä–∏–∏ –¥–ª—è —Å–æ—Ö—Ä–∞–Ω–µ–Ω–∏—è
Procedure.s prepareDir()
  Protected path$
  
  path$ = GetGadgetText(#npt_path)
  If Right(path$, 1) <> "\"
    path$ + "\"
  EndIf 
  
  path$ + SanitizePath(mainStruct\book\name) + "\"
  
  ProcedureReturn CreateFullPath(path$)
EndProcedure

; –ü—Ä–æ–≤–µ—Ä–∫–∞ –∏ –Ω–æ—Ä–º–∞–ª–∏–∑–∞—Ü–∏—è URL
Procedure.s checkURL(url$, *err.err)
  If FindString(url$, "knigavuhe.org/")
    ProcedureReturn ReplaceString(url$, "https://m.", "https://")
  EndIf 
  *err\isErr = 1  
  ProcedureReturn url$
EndProcedure

; –ü–∞—Ä—Å–∏–Ω–≥ –æ–ø–∏—Å–∞–Ω–∏—è –∫–Ω–∏–≥–∏ –∏–∑ HTML
Procedure.s parseDescription(*page.page)
  Protected s$ = findRegExpOne(*page\html, ~"bookDescription\">(.+?)" + "<\/div>", *err.err)
  If CreateRegularExpression(0, "<.*?>")
    s$ = ReplaceRegularExpression(0, s$, "")  ; –£–¥–∞–ª—è–µ–º HTML-—Ç–µ–≥–∏
  EndIf 
  ProcedureReturn s$
EndProcedure

; –§–æ—Ä–º–∏—Ä–æ–≤–∞–Ω–∏–µ –ø–æ–ª–Ω–æ–≥–æ –æ–ø–∏—Å–∞–Ω–∏—è –∫–Ω–∏–≥–∏
Procedure.s getDescription(*page.page, *err.err)
  Protected text$, descripton$  
  text$ = "–ù–∞–∑–≤–∞–Ω–∏–µ: " + mainStruct\book\name + ~"\n"
  text$ + GetGadgetText(#lbl_authors) + " " + GetGadgetText(#src_authors) + ~"\n"
  text$ + GetGadgetText(#lbl_readers) + " " + GetGadgetText(#src_readers) + ~"\n\n–û–ø–∏—Å–∞–Ω–∏–µ:\n"
  
  text$ + parseDescription(*page)
  
  text$ + ~"\n\n" + GetGadgetText(#npt_url)
  ProcedureReturn text$
EndProcedure

; –û—Å–Ω–æ–≤–Ω–∞—è –ø—Ä–æ—Ü–µ–¥—É—Ä–∞ –∑–∞–≥—Ä—É–∑–∫–∏ –∫–Ω–∏–≥–∏
Procedure download_book()
  Protected page.page, path$, url$, error$, err.err
  
  url$ = GetGadgetText(#npt_url)
  url$ = checkURL(url$, @err)
  
  If Len(GetGadgetText(#npt_path)) = 0
    MessageRequester("–û—à–∏–±–∫–∞", "–ù–µ–∫–æ—Ä–µ–∫—Ç–Ω—ã–π –ø—É—Ç—å –∫ –ø–∞–ø–∫–µ", #PB_MessageRequester_Error)
    ProcedureReturn
  EndIf 
  
  If err\isErr
    MessageRequester("–û—à–∏–±–∫–∞", ~"–ù–µ–∫–æ—Ä—Ä–µ–∫—Ç–Ω—ã–π –∞–¥—Ä–µ—Å URL:\n" + url$, #PB_MessageRequester_Error)
    ProcedureReturn 
  EndIf 
  
  SetGadgetText(#src_info, "–ó–∞–≥—Ä—É–∑–∫–∞ —Å—Ç—Ä–∞–Ω–∏—Ü—ã")
  
  ; –ó–∞–≥—Ä—É–∂–∞–µ–º —Å—Ç—Ä–∞–Ω–∏—Ü—É –∫–Ω–∏–≥–∏
  page\html = downloadPage(url$, @err)
  If err\isErr
    MessageRequester("–û—à–∏–±–∫–∞", ~"–ù–µ —É–¥–∞–ª–æ—Å—å —Å–∫–∞—á–∞—Ç—å —Å—Ç—Ä–∞–Ω–∏—Ü—É:\n" + url$, #PB_MessageRequester_Error)
    ProcedureReturn 
  EndIf 
  
  ; –û–±—Ä–∞–±–∞—Ç—ã–≤–∞–µ–º —Å—Ç—Ä–∞–Ω–∏—Ü—É
  result$ = processPage(@page, @err)
  If err\isErr
    MessageRequester("–û—à–∏–±–∫–∞", ~"–ù–µ —É–¥–∞–ª–æ—Å—å –æ–±—Ä–∞–±–æ—Ç–∞—Ç—å —Å—Ç—Ä–∞–Ω–∏—Ü—É:\n" + result$, #PB_MessageRequester_Error)
    ProcedureReturn 
  EndIf 
  
  ; –ü–æ–¥–≥–æ—Ç–∞–≤–ª–∏–≤–∞–µ–º –¥–∏—Ä–µ–∫—Ç–æ—Ä–∏—é
  path$ = prepareDir()
  
  ; –ó–∞–≥—Ä—É–∂–∞–µ–º –æ–±–ª–æ–∂–∫—É (–µ—Å–ª–∏ –µ—â–µ –Ω–µ —Å—É—â–µ—Å—Ç–≤—É–µ—Ç)
  If Not fileExists(path$, "cover.jpg")
    SetGadgetText(#src_info, "–ó–∞–≥—Ä—É–∑–∫–∞ –æ–±–ª–æ–∂–∫–∏")
    
    ; –ü—Ä–æ–±—É–µ–º –∞–ª—å—Ç–µ—Ä–Ω–∞—Ç–∏–≤–Ω—ã–π URL –æ–±–ª–æ–∂–∫–∏
    error$ = downFile(mainStruct\book\cover_alt, path$+"cover.jpg")
    If error$ <> ""
      error$ = downFile(mainStruct\book\cover, path$+"cover.jpg")
    EndIf 
    
    If error$ <> ""
      MessageRequester("–ü—Ä–µ–¥—É–ø—Ä–µ–∂–¥–µ–Ω–∏–µ", ~"–ù–µ —É–¥–∞–ª–æ—Å—å –∑–∞–≥—Ä—É–∑–∏—Ç—å –æ–±–ª–æ–∂–∫—É: \n" + error$, #PB_MessageRequester_Warning)
    EndIf 
    SetGadgetText(#tracker, "–ì–æ—Ç–æ–≤–æ")
  EndIf 
  
  SetGadgetText(#btn_down, "–ü—Ä–µ—Ä–≤–∞—Ç—å")
  
  ; –°–æ–∑–¥–∞–µ–º —Ñ–∞–π–ª –æ–ø–∏—Å–∞–Ω–∏—è (–µ—Å–ª–∏ –µ—â–µ –Ω–µ —Å—É—â–µ—Å—Ç–≤—É–µ—Ç)
  If Not fileExists(path$, "Description.txt")
    SetGadgetText(#src_info, "–°–æ–∑–¥–∞–Ω–∏–µ —Ñ–∞–π–ª–∞ –æ–ø–∏—Å–∞–Ω–∏—è")
    descripton$ = getDescription(@page, @err)
    updateDecription(path$+"Description.txt", descripton$)
    SetGadgetText(#tracker, "–ì–æ—Ç–æ–≤–æ")
  EndIf 
  
  ClearStructure(@page, page)
  
  ; –ó–∞–≥—Ä—É–∂–∞–µ–º –ø–ª–µ–π–ª–∏—Å—Ç
  error$ = downPlaylist(path$)
  SetGadgetText(#tracker, "")
  SetGadgetText(#btn_down, "–°–∫–∞—á–∞—Ç—å")
  
  If Not stop
    SetGadgetText(#npt_url, "")
  EndIf 
  
  ; –í—ã–≤–æ–¥–∏–º —Ä–µ–∑—É–ª—å—Ç–∞—Ç –∑–∞–≥—Ä—É–∑–∫–∏
  If Len(error$) <> 0
    If stop
      MessageRequester("–ü—Ä–µ–¥—É–ø—Ä–µ–∂–¥–µ–Ω–∏–µ", error$, #PB_MessageRequester_Warning)
    Else
      MessageRequester("–û—à–∏–±–∫–∞", error$, #PB_MessageRequester_Error)
    EndIf     
  Else
    If #PB_MessageRequester_Yes = MessageRequester("Done!", ~"–ó–∞–≥—Ä—É–∑–∫–∞ —É—Å–ø–µ—à–Ω–æ –∑–∞–≤–µ—Ä—à–µ–Ω–∞\n–û—Ç–∫—Ä—ã—Ç—å –ø–∞–ø–∫—É —Å –∫–Ω–∏–≥–æ–π?", #PB_MessageRequester_Info|#PB_MessageRequester_YesNo )
      RunProgram("explorer.exe", ~"\"" + path$ +~"\"", "")
    EndIf
  EndIf
EndProcedure

; –û—Ç–∫–ª—é—á–µ–Ω–∏–µ —ç–ª–µ–º–µ–Ω—Ç–æ–≤ GUI
Procedure disableGui(state)
  DisableGadget(#npt_url, state)
  DisableGadget(#npt_path, state)
  DisableGadget(#btn_path, state)
EndProcedure

; –ó–∞–ø—É—Å–∫ –∑–∞–≥—Ä—É–∑–∫–∏ –≤ –æ—Ç–¥–µ–ª—å–Ω–æ–º –ø–æ—Ç–æ–∫–µ
Procedure StartDownloading(o)
  downloading = 1
  stop = 0
  
  disableGui(#True)
  
  download_book()
  
  ClearStructure(@mainStruct, main)
  
  downloading = 0
  stop = 0
  disableGui(#False)
EndProcedure

; –°–æ–∑–¥–∞–Ω–∏–µ –≥–ª–∞–≤–Ω–æ–≥–æ –æ–∫–Ω–∞
Procedure Openmain_window(x = 0, y = 0, width = 504, height = 272)
  OpenWindow(#main_window, x, y, width, height, "Downloader knigavuhe.org", #PB_Window_SystemMenu | #PB_Window_ScreenCentered)
  FrameGadget(#PB_Any, 8, 8, 488, 48, "–°—Å—ã–ª–∫–∞")
  StringGadget(#npt_url, 16, 24, 472, 24, "")
  FrameGadget(#PB_Any, 8, 64, 488, 48, "–ü–∞–ø–∫–∞ –¥–ª—è —Å–æ—Ö—Ä–∞–Ω–µ–Ω–∏—è –∫–Ω–∏–≥–∏")
  StringGadget(#npt_path, 16, 80, 432, 24, "")
  ButtonGadget(#btn_path, 456, 80, 32, 24, "üìÅ")
  TextGadget(#PB_Any, 16, 128, 64, 20, "–ù–∞–∑–≤–∞–Ω–∏–µ:", #PB_Text_Right)
  TextGadget(#lbl_authors, 16, 152, 64, 20, "–ê–≤—Ç–æ—Ä:", #PB_Text_Right)
  TextGadget(#lbl_readers, 16, 176, 64, 20, "–ß—Ç–µ—Ü:", #PB_Text_Right)
  ProgressBarGadget(#progress_bar, 16, 240, 472, 20, 0, 100)
  TextGadget(#src_book_name, 88, 128, 400, 20, "")
  TextGadget(#src_authors, 88, 152, 312, 20, "")
  TextGadget(#src_readers, 88, 176, 312, 20, "")
  TextGadget(#src_info, 16, 208, 314, 20, "")
  TextGadget(#tracker, 330, 208, 158, 20, "", #PB_Text_Right)
  ButtonGadget(#btn_down, 408, 160, 80, 28, "–°–∫–∞—á–∞—Ç—å")
  
  ; –£—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø—É—Ç—å –ø–æ —É–º–æ–ª—á–∞–Ω–∏—é
  SetGadgetText(#npt_path, GetEnvironmentVariable("USERPROFILE") + "\Music\")
EndProcedure

; –û—Å–Ω–æ–≤–Ω–æ–π —Ü–∏–∫–ª –ø—Ä–æ–≥—Ä–∞–º–º—ã
Openmain_window()

Repeat
  Define event = WaitWindowEvent()
  Select event
    Case #PB_Event_Gadget
      gadget = EventGadget()
      Select gadget
        Case #btn_path
          path_button()
          
        Case #btn_down
          If downloading
            stop = 1  ; –ü—Ä–µ—Ä—ã–≤–∞–µ–º –∑–∞–≥—Ä—É–∑–∫—É
          Else            
            CreateThread(@StartDownloading(), 0)  ; –ó–∞–ø—É—Å–∫–∞–µ–º –≤ –æ—Ç–¥–µ–ª—å–Ω–æ–º –ø–æ—Ç–æ–∫–µ
          EndIf 
      EndSelect
      
    Case #PB_Event_CloseWindow
      Break
  EndSelect
ForEver
CloseWindow(0)

End
