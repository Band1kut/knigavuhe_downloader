Enumeration FormWindow
  #main_window
EndEnumeration

Enumeration FormGadget
  #npt_url
  #npt_path
  #btn_path
  #lbl_authors
  #lbl_readers
  #progress_bar
  #src_book_name
  #src_authors
  #src_readers
  #src_info
  #tracker
  #btn_down
EndEnumeration

Structure err
  isErr.b
EndStructure

Structure names
  name.s
EndStructure

Structure book
  name.s
  Map authors.names()
  Map readers.names()
  cover.s
  url.s
EndStructure

Structure audio
  title.s
  url.s
EndStructure

Structure main
  id.i
  book.book
  List playlist.audio()
  List merged_playlist.audio()
EndStructure

Structure page
  html.s
EndStructure

Global mainStruct.main, stop.b

Procedure updateDecription(path$, text$)
  If OpenFile(0, path$)
    FileSeek(0, Lof(0))
    WriteStringN(0, text$)
    CloseFile(0)
  EndIf
EndProcedure

Procedure.s findRegExpOne(text$, exp$, *err.err)
  Protected result$, re
  re = CreateRegularExpression(#PB_Any, exp$, #PB_RegularExpression_DotAll)
  If re
    If ExamineRegularExpression(re, text$)
      NextRegularExpressionMatch(re)
      result$ = RegularExpressionGroup(re, 1)
    Else
      *err\isErr = 1
      result$ = "–û—à–∏–±–∫–∞ —Ä–µ–≥—É–ª—è—Ä–Ω–æ–≥–æ –≤—ã—Ä–∞–∂–µ–Ω–∏—è:" + RegularExpressionError()
    EndIf
  Else
    *err\isErr = 1
    result$ = "–û—à–∏–±–∫–∞ —Ä–µ–≥—É–ª—è—Ä–Ω–æ–≥–æ –≤—ã—Ä–∞–∂–µ–Ω–∏—è:" + RegularExpressionError()
  EndIf
  
  FreeRegularExpression(re)
  
  ProcedureReturn result$
EndProcedure

Procedure.s getPartString(s$, sep$, idx=0)
  Protected c
  
  s$ = RTrim(s$, sep$)
  c = CountString(s$, sep$)
  
  If idx = -1
    ProcedureReturn StringField(s$, c+1, sep$)
  ElseIf idx = 0
    ProcedureReturn s$
  EndIf 
  
  ProcedureReturn StringField(s$, c+1, sep$)
    
EndProcedure

Procedure.b prepareJSON(json$)
  If ParseJSON(0, json$)
    ExtractJSONStructure(JSONValue(0), mainStruct.main, main)
    FreeJSON(0)
    ProcedureReturn #True
  EndIf 
  ProcedureReturn #False
EndProcedure


Procedure.s downloadPage(url$, *err.err)
  Protected html$, NewMap Header$()
  Header$("cookie") = "new_design=1"
  Header$("User-Agent") = "Mozilla/5.0 (Windows NT 10.0; Win64; x64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/135.0.0.0 Safari/537.36 Edg/135.0.0.0"
  
  HttpRequest = HTTPRequest(#PB_HTTP_Get, url$, "", 0, Header$())
  If HttpRequest
    If HTTPInfo(HTTPRequest, #PB_HTTP_StatusCode) = "200"
      *BufPage = HTTPMemory(HTTPRequest)
      html$ = PeekS(*BufPage, MemorySize(*BufPage),  #PB_UTF8|#PB_ByteLength)
      FreeMemory(*BufPage)
    Else
      *err\isErr = 1
      html$ = ~"–û—à–∏–±–∫–∞ –≤—ã–ø–æ–ª–Ω–µ–Ω–∏—è –∑–∞–ø—Ä–æ—Å–∞: \n"+url$+~"\n"+HTTPInfo(HTTPRequest, #PB_HTTP_ErrorMessage)
    EndIf 
  Else
    *err\isErr = 1
    html$ = ~"–û—à–∏–±–∫–∞ —Å–æ–∑–¥–∞–Ω–∏—è –∑–∞–ø—Ä–æ—Å–∞: \n"+url$
  EndIf 
  
  FinishHTTP(HttpRequest)
  FreeMap(Header$())
  ProcedureReturn html$
EndProcedure

Procedure.s getAuthors(Map tMap.names())
  Protected authors$
  ResetMap(tMap())

    While NextMapElement(tMap())
      If Len(authors$) > 0
        authors$ + ", " + tMap()\name
      Else
        authors$ = tMap()\name
      EndIf
    Wend
    ProcedureReturn authors$
EndProcedure

Procedure.s processPage(*page.page, *err.err)
  ;   Debug GetGadgetText(#npt_url)
  Protected json$, author$, reader$
  json$ = findRegExpOne(*page\html, "BookController\.enter\((.*?\})\);", *err)
  
  If json$
    If Not prepareJSON(json$)
      ProcedureReturn ~"–ù–µ —É–¥–∞–ª–æ—Å—å –æ–±—Ä–∞–±–æ—Ç–∞—Ç—å JSON:\n" + json$
    EndIf 
    
    SetGadgetText(#src_book_name, mainStruct\book\name)
    
    author$ = getAuthors(mainStruct\book\authors())
    SetGadgetText(#src_authors, author$)
    
    If FindString(author$, ", ")
      SetGadgetText(#lbl_authors, "–ê–≤—Ç–æ—Ä—ã:")
    Else
      SetGadgetText(#lbl_authors, "–ê–≤—Ç–æ—Ä:")
    EndIf 
    
    reader$ = getAuthors(mainStruct\book\readers())
    SetGadgetText(#src_readers, reader$)
    
    If FindString(reader$, ", ")
      SetGadgetText(#lbl_readers, "–ß—Ç–µ—Ü—ã:")
    Else
      SetGadgetText(#lbl_readers, "–ß—Ç–µ—Ü:")
    EndIf 
    
  Else
    ProcedureReturn ~"–ù–µ —É–¥–∞–ª–æ—Å—å –ø–æ–ª—É—á–∏—Ç—å JSON:\n" + json$
  EndIf
  
  ProcedureReturn ""
EndProcedure

Procedure path_button()
  Protected defaultPath$, path$
  defaultPath$ = GetEnvironmentVariable("USERPROFILE") + "\Music\"  
  path$ = PathRequester("–í—ã–±–µ—Ä–∏—Ç–µ –ø–∞–ø–∫—É –¥–ª—è —Å—Ö—Ä–∞–Ω–µ–Ω–∏—è", defaultPath$)
  
  If path$ 
    SetGadgetText(#npt_path, path$)
    ProcedureReturn
  EndIf
  
  If Len(GetGadgetText(#npt_path)) = 0
    MessageRequester("–ü—Ä–µ–¥—É–ø—Ä–µ–∂–¥–µ–Ω–∏–µ", "–ü—É—Ç—å —Å–æ—Ö—Ä–∞–Ω–µ–Ω–∏—è –Ω–µ –º–æ–∂–µ—Ç –±—ã—Ç—å –ø—É—Å—Ç—ã–º", #PB_MessageRequester_Warning)
  EndIf 
  
EndProcedure

Procedure.s ConvertBytes(bytes)
  Protected result.s
  If bytes >= 1048576
    result = StrF(bytes / 1048576.0, 2) + "mb"
  ElseIf bytes >= 1024
    result = StrF(bytes / 1024.0, 2) + "kb"
  Else
    result = Str(bytes) + "b"
  EndIf
  ProcedureReturn result
EndProcedure

Procedure GetContentLength(url$)
  Protected file_size, Header$
  HttpRequest = HTTPRequest(#PB_HTTP_Get, url$, "", #PB_HTTP_HeadersOnly)
  If HttpRequest
    Header$ = HTTPInfo(HTTPRequest, #PB_HTTP_Headers)
    Header$ = StringField(Header$, 2, "Content-Length:")
    file_size = Val(StringField(Header$, 1, #LF$))
    FinishHTTP(HTTPRequest)
  EndIf 
  
  ProcedureReturn file_size
EndProcedure

Procedure.s downFile(url$, name_file$, size_file=0)
  Protected content_length, Download, Progress, result$
  
  Download = ReceiveHTTPFile(url$, name_file$, #PB_HTTP_Asynchronous)
  If Download
    Repeat
      If stop
        AbortHTTP(Download)
      EndIf 
      
      Progress = HTTPProgress(Download)
      Select Progress
        Case #PB_HTTP_Success
          Break
          
        Case #PB_HTTP_Failed
          result$ = "–ó–∞–≥—Ä—É–∑–∫–∞ –Ω–µ —É–¥–∞–ª–∞—Å—å"
          Break
          
        Case #PB_HTTP_Aborted
          result$ = "–ó–∞–≥—Ä—É–∑–∫–∞ –ø—Ä–µ—Ä–≤–∞–Ω–∞"
          
          Break
          
        Default
          If size_file
            SetGadgetText(#tracker, ConvertBytes(Progress)+ "/" +ConvertBytes(size_file))
            SetGadgetState(#progress_bar, Int(Progress/size_file*100))            
          EndIf 
          
      EndSelect
      
      Delay(50)
    ForEver
    FinishHTTP(Download)
  Else
    result$ = "–û—à–∏–±–∫–∞ –∑–∞–≥—Ä—É–∑–∫–∏"
  EndIf
  
  If size_file
    SetGadgetText(#tracker, ConvertBytes(size_file)+ "/" +ConvertBytes(size_file))
    SetGadgetState(#progress_bar, 0)
  EndIf
  ProcedureReturn result$
EndProcedure

Procedure delFilesByExt(path$, ext$)
  If ExamineDirectory(0, path$, ext$)
    While NextDirectoryEntry(0)
      If DirectoryEntryType(0) = #PB_DirectoryEntry_File
        file$ = path$ + DirectoryEntryName(0)
        If Not DeleteFile(file$)
          RenameFile(file$, file$+".delete")
        EndIf
      EndIf
    Wend
    FinishDirectory(0)
  Else
    MessageRequester("–û—à–∏–±–∫–∞", "–ù–µ —É–¥–∞–ª–æ—Å—å –æ—Ç–∫—Ä—ã—Ç—å –ø–∞–ø–∫—É: " + directory$)
  EndIf
EndProcedure

Procedure.b fileExists(path$, name_file$, size_file=0)
  Protected result.b
  If ExamineDirectory(0, path$, name_file$)
    If NextDirectoryEntry(0)
      
      If Not size_file
        result = #True
      ElseIf DirectoryEntryType(0) = #PB_DirectoryEntry_File  
        If size_file = DirectoryEntrySize(0)
          result = #True
        EndIf
      EndIf
      
    EndIf 
    FinishDirectory(0)
  Else
    MessageRequester("–û—à–∏–±–∫–∞", "–ù–µ —É–¥–∞–ª–æ—Å—å –æ—Ç–∫—Ä—ã—Ç—å –ø–∞–ø–∫—É: " + directory$)
  EndIf
  ProcedureReturn result
EndProcedure

Procedure.s iterPlaylist(path$, List pl.audio())
  Protected name_file$, path_file$, count, total$, error$, ext$, size_file
  total$ = Str(ListSize(pl()))
  
  ResetList(pl())
  ForEach pl()
    count +1
    SetGadgetText(#src_info, Str(count) + "/" + total$ + " " + pl()\title)
    
    pl()\url = getPartString(pl()\url, "?", 1)
    ext$ = "."+getPartString(pl()\url, ".", -1)
    name_file$ = pl()\title + ext$
    
    If count = 1
      name_file$ = "."+mainStruct\book\name + ext$
    EndIf 
    
    path_file$ = path$ + name_file$
    size_file = GetContentLength(pl()\url)
    
    If fileExists(path$, name_file$, size_file)
      SetGadgetText(#tracker, "–£–∂–µ —Å–∫–∞—á–∞–Ω")
      Delay(700)
      Continue
    EndIf 
    
    error$ = downFile(pl()\url, path_file$, size_file)
    If Len(error$ ) <> 0
      error$ + ~"\n" + "–°–∫–∞—á–∞–Ω–æ " + Str(count-1) + " –∞—É–¥–∏–æ—Ñ–∞–π–ª–æ–≤ –∏–∑ " + total$
      Break
    EndIf 
;     updateDecription(path$, name_file$)
    
  Next
  ProcedureReturn error$
EndProcedure

Procedure.s downPlaylist(path$)
  Protected error$
  
  error$ = iterPlaylist(path$, mainStruct\playlist())
  If Len(error$) = 0 Or stop
    ProcedureReturn error$
  EndIf 
  
  error$ + ~"\n\n –ó–∞–≥—Ä—É–∑–∫–∞ –Ω–µ—á–Ω–µ—Ç—Å—è –∑–∞–Ω–æ–≤–æ –∏–∑ –∞–ª—å—Ç–µ—Ä–Ω–∞—Ç–∏–≤–Ω–æ–≥–æ –∏—Å—Ç–æ—á–Ω–∏–∫–∞"
  MessageRequester("–û—à–∏–±–∫–∞", error$, #PB_MessageRequester_Error)
  
  delFilesByExt(path$, "*.mp3")
  
  error$ = iterPlaylist(path$, mainStruct\merged_playlist())
  
  If Len(error$) <> 0
    error$ + ~"\n\n –ó–∞–≥—Ä—É–∑–∫–∞ –Ω–µ–≤–æ–∑–º–æ–∂–Ω–∞ –∏–∑ –æ–±–æ–∏—Ö –∏—Å—Ç–æ—á–Ω–∏–∫–æ–≤ :("
  EndIf 
  
  ProcedureReturn error$
EndProcedure

Procedure.s CreateFullPath(path$)
  Protected Parts$, i, CurrentPath$
  
  If Right(path$, 1) = "\" Or Right(path$, 1) = "/"
    path$ = Left(path$, Len(path$) - 1)
  EndIf
  
  path$ = ReplaceString(path$, "/", "\")
  
  Parts$ = StringField(path$, 1, "\")
  CurrentPath$ = Parts$
  
  For i = 2 To CountString(path$, "\") + 1
    Parts$ = StringField(path$, i, "\")
    CurrentPath$ + "\" + Parts$
    If FileSize(CurrentPath$) <> -2
      CreateDirectory(CurrentPath$)
    EndIf
  Next
  
    ProcedureReturn path$ + "\"  
EndProcedure

Procedure.s prepareDir()
  Protected path$
  
  path$ = GetGadgetText(#npt_path)
  If Right(path$, 1) <> "\"
    path$ + "\"
  EndIf 
  
  path$ + getPartString(mainStruct\book\url, "/", -1) + "\"
  
  ProcedureReturn CreateFullPath(path$)
EndProcedure

Procedure.s checkURL(url$, *err.err)
  If FindString(url$, "knigavuhe.org/")
    ProcedureReturn ReplaceString(url$, "https://m.", "https://")
  EndIf 
  *err\isErr = 1  
  ProcedureReturn url$
EndProcedure

Procedure.s parseDescription(*page.page)
  Protected s$ = findRegExpOne(*page\html, ~"bookDescription\">(.+?)" + "<\/div>", *err.err)
  If CreateRegularExpression(0, "<.*?>")
    s$ = ReplaceRegularExpression(0, s$, "")
  EndIf 
  ProcedureReturn s$
EndProcedure

Procedure.s getDescription(*page.page, *err.err)
  Protected text$, descripton$  
  text$ = "–ù–∞–∑–≤–∞–Ω–∏–µ: " + mainStruct\book\name + ~"\n"
  text$ + GetGadgetText(#lbl_authors) + " " + GetGadgetText(#src_authors) + ~"\n"
  text$ + GetGadgetText(#lbl_readers) + " " + GetGadgetText(#src_readers) + ~"\n\n–û–ø–∏—Å–∞–Ω–∏–µ:\n"
  
  text$ + parseDescription(*page);~"bookDescription\">(.*?)</div>"
  
  text$ + ~"\n\n" + GetGadgetText(#npt_url)
  ProcedureReturn text$
EndProcedure


Procedure download_book()
  Protected page.page, path$, url$,  error$, err.err
  
  url$ = GetGadgetText(#npt_url)
  url$ = checkURL(url$, @err)
  
  If Len(GetGadgetText(#npt_path)) = 0
    MessageRequester("–û—à–∏–±–∫–∞", "–ù–µ–∫–æ—Ä–µ–∫—Ç–Ω—ã–π –ø—É—Ç—å –∫ –ø–∞–ø–∫–µ", #PB_MessageRequester_Error)
    ProcedureReturn
  EndIf 
  
  If err\isErr
    MessageRequester("–û—à–∏–±–∫–∞", ~"–ù–µ–∫–æ—Ä—Ä–µ–∫—Ç–Ω—ã–π –∞–¥—Ä–µ—Å URL:\n" + url$, #PB_MessageRequester_Error)
    ProcedureReturn 
  EndIf 
  
  SetGadgetText(#src_info, "–ó–∞–≥—Ä—É–∑–∫–∞ —Å—Ç—Ä–∞–Ω–∏—Ü—ã")
  
  page\html = downloadPage(url$, @err)
  If err\isErr
    MessageRequester("–û—à–∏–±–∫–∞", ~"–ù–µ —É–¥–∞–ª–æ—Å—å —Å–∫–∞—á–∞—Ç—å —Å—Ç—Ä–∞–Ω–∏—Ü—É:\n" + url$, #PB_MessageRequester_Error)
    ProcedureReturn 
  EndIf 
  
  result$ = processPage(@page, @err)
  If err\isErr
    MessageRequester("–û—à–∏–±–∫–∞", ~"–ù–µ —É–¥–∞–ª–æ—Å—å –æ–±—Ä–∞–±–æ—Ç–∞—Ç—å —Å—Ç—Ä–∞–Ω–∏—Ü—É:\n" + result$, #PB_MessageRequester_Error)
    ProcedureReturn 
  EndIf 
  
  path$ = prepareDir()
  
  
  If Not fileExists(path$, "cover.jpg")
    SetGadgetText(#src_info, "–ó–∞–≥—Ä—É–∑–∫–∞ –æ–±–ª–æ–∂–∫–∏")
    error$ = downFile(mainStruct\book\cover, path$+"cover.jpg")
    If error$ <> ""
      MessageRequester("–ü—Ä–µ–¥—É–ø—Ä–µ–∂–¥–µ–Ω–∏–µ", ~"–ù–µ —É–¥–∞–ª–æ—Å—å –∑–∞–≥—Ä—É–∑–∏—Ç—å –æ–±–ª–æ–∂–∫—É: \n" + error$, #PB_MessageRequester_Warning)
    EndIf 
    SetGadgetText(#tracker, "–ì–æ—Ç–æ–≤–æ")
  EndIf 
  
  SetGadgetText(#btn_down, "–ü—Ä–µ—Ä–≤–∞—Ç—å")
  
  If Not fileExists(path$, "Description.txt")
    SetGadgetText(#src_info, "–°–æ–∑–¥–∞–Ω–∏–µ —Ñ–∞–π–ª–∞ –æ–ø–∏—Å–∞–Ω–∏—è")
    descripton$ = getDescription(@page, @err)
    updateDecription(path$+"Description.txt", descripton$)
    SetGadgetText(#tracker, "–ì–æ—Ç–æ–≤–æ")
  EndIf 
  
  ClearStructure(@page, page)
  
  error$ = downPlaylist(path$)
  SetGadgetText(#tracker, "")
  SetGadgetText(#btn_down, "–°–∫–∞—á–∞—Ç—å")
  SetGadgetText(#npt_url, "")
  
  If Len(error$) <> 0
    MessageRequester("–û—à–∏–±–∫–∞", error$, #PB_MessageRequester_Error)
  Else
    If #PB_MessageRequester_Yes = MessageRequester("Done!", ~"–ó–∞–≥—Ä—É–∑–∫–∞ —É—Å–ø–µ—à–Ω–æ –∑–∞–≤–µ—Ä—à–µ–Ω–∞\n–û—Ç–∫—Ä—ã—Ç—å –ø–∞–ø–∫—É —Å –∫–Ω–∏–≥–æ–π?", #PB_MessageRequester_Info|#PB_MessageRequester_YesNo )
      RunProgram("explorer.exe", path$, "")
    EndIf
  EndIf
  
EndProcedure

Procedure StartDownloading(o)
  downloading = 1
  stop = 0
  
  download_book()
  
  ClearStructure(@mainStruct, main)
  
  downloading = 0
  stop = 0
EndProcedure

Procedure Openmain_window(x = 0, y = 0, width = 504, height = 272)
  OpenWindow(#main_window, x, y, width, height, "Downloader knigavuhe.org", #PB_Window_SystemMenu | #PB_Window_ScreenCentered)
  FrameGadget(#PB_Any, 8, 8, 488, 48, "–°—Å—ã–ª–∫–∞")
  StringGadget(#npt_url, 16, 24, 472, 24, "")
  FrameGadget(#PB_Any, 8, 64, 488, 48, "–ü–∞–ø–∫–∞ –¥–ª—è —Å–æ—Ö—Ä–∞–Ω–µ–Ω–∏—è –∫–Ω–∏–≥–∏")
  StringGadget(#npt_path, 16, 80, 432, 24, "")
  ButtonGadget(#btn_path, 456, 80, 32, 24, "üìÅ")
  TextGadget(#PB_Any, 16, 128, 64, 20, "–ù–∞–∑–≤–∞–Ω–∏–µ:", #PB_Text_Right)
  TextGadget(#lbl_authors, 16, 152, 64, 20, "–ê–≤—Ç–æ—Ä:", #PB_Text_Right)
  TextGadget(#lbl_readers, 16, 176, 64, 20, "–ß—Ç–µ—Ü:", #PB_Text_Right)
  ProgressBarGadget(#progress_bar, 16, 240, 472, 20, 0, 100)
  TextGadget(#src_book_name, 88, 128, 400, 20, "")
  TextGadget(#src_authors, 88, 152, 312, 20, "")
  TextGadget(#src_readers, 88, 176, 312, 20, "")
  TextGadget(#src_info, 16, 208, 314, 20, "")
  TextGadget(#tracker, 330, 208, 158, 20, "", #PB_Text_Right)
  ButtonGadget(#btn_down, 408, 160, 80, 28, "–°–∫–∞—á–∞—Ç—å")
  
  SetGadgetText(#npt_path, GetEnvironmentVariable("USERPROFILE") + "\Music\")
  
EndProcedure

Openmain_window()

Repeat
  Define event = WaitWindowEvent()
  Select event
    Case #PB_Event_Gadget
      gadget = EventGadget()
      Select gadget
          
        Case #btn_path
          path_button()
          
        Case #btn_down
          
          If downloading
            stop = 1
          Else            
            CreateThread(@StartDownloading(), 0)
          EndIf 
          
      EndSelect
      
    Case #PB_Event_CloseWindow
      Break
  EndSelect
ForEver
CloseWindow(0)

End

  
; IDE Options = PureBasic 6.10 LTS (Windows - x64)
; CursorPosition = 252
; FirstLine = 225
; Folding = ----
; Optimizer
; EnableThread
; EnableXP
; DPIAware
; UseIcon = icon.ico
; Executable = downloader knigavuhe.org.exe
; Compiler = PureBasic 6.10 LTS - C Backend (Windows - x64)
